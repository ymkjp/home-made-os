; haribote-os boot asm
; TAB=4

[INSTRSET "i486p"]

; 絶対に同じ数値を指定しない
VBEMODE1	EQU		0x117			; 第1候補 XGA16bitカラー
VBEMODE2	EQU		0x105			; 第2候補 XGA 8bitカラー

BOTPAK	EQU		0x00280000		; bootpackのロード先
DSKCAC	EQU		0x00100000		; ディスクキャッシュの場所
DSKCAC0	EQU		0x00008000		; ディスクキャッシュの場所（リアルモード）

; BOOT_INFO関係
CYLS	EQU		0x0ff0			; ブートセクタが設定する
LEDS	EQU		0x0ff1
VMODE	EQU		0x0ff2			; 色数に関する情報。何ビットカラーか？
SCRNX	EQU		0x0ff4			; 解像度のX
SCRNY	EQU		0x0ff6			; 解像度のY
VRAM	EQU		0x0ff8			; グラフィックバッファの開始番地

		ORG		0xc200			; このプログラムがどこに読み込まれるのか

; 画面設定::VBEがあるか
		MOV		AX,0x9000
		MOV		ES,AX
		MOV		DI,0
		MOV		AX,0x4f00
		INT 	0x10
		CMP		AX,0x004f
		JNE		scrn320

; 画面設定::VBEバージョンチェック
		MOV		AX,[ES:DI+4]
		CMP		AX,0x0200
		JB		scrn320

; 画面設定::VBEMODEで指定したモードの情報を取得
		MOV		CX,VBEMODE1				; 第1候補
scrn_retry:								; 第2候補はここから
		MOV		AX,0x4f01
		INT		0x10
		CMP		AX,0x004f
		JNE		scrn_next				; 次の候補へ

; 画面設定::画面モード情報の確認
		MOV		AX,[ES:DI+0x00]			; 8bit, 16bit 共通チェック項目
		AND		AX,0x0080
		JZ		scrn_next
		MOV		AX,CX
		AND		AX,0x0010				; 8ビットカラーか16ビットカラーか調べる
		CMP		AX,0x0010
		JE		scrn_16bit				; 16ビットの場合はジャンプ
		MOV		DL,8					;  8ビットの画面モード確認
		CMP		BYTE [ES:DI+0x19],8
		JNE		scrn_next
		CMP		BYTE [ES:DI+0x1b],4
		JNE		scrn_next

; 画面設定::モード切替
scrn_set:
		ADD		CX,0x4000
		MOV		BX,CX					;	MOV		BX,VBEMODE1+0x4000
		SUB		CX,0x4000
		MOV		AX,0x4f02
		INT		0x10
		MOV		BYTE [VMODE],DL			; 画面モードをメモする（C言語が参照する）
		MOV		AX,[ES:DI+0x12]
		MOV		[SCRNX],AX
		MOV		AX,[ES:DI+0x14]
		MOV		[SCRNY],AX
		MOV		EAX,[ES:DI+0x28]
		MOV		[VRAM],EAX
		JMP		keystatus

; 候補切り替え
scrn_next:
		CMP		CX,VBEMODE1				; 2回目へ
		JE		scrn_next1to2
		CMP		CX,VBEMODE2				; 2回目もだめなのでscrn320へ
		JE		scrn320
scrn_next1to2:
		MOV		CX,VBEMODE2
		JMP		scrn_retry

; 16ビットの画面モード確認
scrn_16bit:
		MOV		DL,16
		CMP		BYTE [ES:DI+0x19],16
		JNE		scrn_next
		CMP		BYTE [ES:DI+0x1b],6
		JNE		scrn_next
		JMP		scrn_set

; 画面設定::VBEMODEで指定したモードが使えなかった
scrn320:
		MOV		AL,0x13					; VGAグラフィックス、320x200x8bitカラー
		MOV		AH,0x00
		INT		0x10
		MOV		BYTE [VMODE],8			; 画面モードをメモする（C言語が参照する）
		MOV		WORD [SCRNX],320
		MOV		WORD [SCRNY],200
		MOV		DWORD [VRAM],0x000a0000

; キーボードのLED状態をBIOSに教えてもらう
keystatus:
		MOV		AH,0x02
		INT		0x16 			; keyboard BIOS
		MOV		[LEDS],AL

; PICが一切の割り込みを受け付けないようにする
;	AT互換機の仕様では、PICの初期化をするなら、
;	こいつをCLI前にやっておかないと、たまにハングアップする
;	PICの初期化はあとでやる

		MOV		AL,0xff
		OUT		0x21,AL
		NOP						; OUT命令を連続させるとうまくいかない機種があるらしいので
		OUT		0xa1,AL

		CLI						; さらにCPUレベルでも割り込み禁止

; CPUから1MB以上のメモリにアクセスできるように、A20GATEを設定

		CALL	waitkbdout
		MOV		AL,0xd1
		OUT		0x64,AL
		CALL	waitkbdout
		MOV		AL,0xdf			; enable A20
		OUT		0x60,AL
		CALL	waitkbdout

; プロテクトモード移行


		LGDT	[GDTR0]			; 暫定GDTを設定
		MOV		EAX,CR0
		AND		EAX,0x7fffffff	; bit31を0にする（ページング禁止のため）
		OR		EAX,0x00000001	; bit0を1にする（プロテクトモード移行のため）
		MOV		CR0,EAX
		JMP		pipelineflash
pipelineflash:
		MOV		AX,1*8			;  読み書き可能セグメント32bit
		MOV		DS,AX
		MOV		ES,AX
		MOV		FS,AX
		MOV		GS,AX
		MOV		SS,AX

; bootpackの転送

		MOV		ESI,bootpack	; 転送元
		MOV		EDI,BOTPAK		; 転送先
		MOV		ECX,512*1024/4
		CALL	memcpy

; ついでにディスクデータも本来の位置へ転送

; まずはブートセクタから

		MOV		ESI,0x7c00		; 転送元
		MOV		EDI,DSKCAC		; 転送先
		MOV		ECX,512/4
		CALL	memcpy

; 残り全部

		MOV		ESI,DSKCAC0+512	; 転送元
		MOV		EDI,DSKCAC+512	; 転送先
		MOV		ECX,0
		MOV		CL,BYTE [CYLS]
		IMUL	ECX,512*18*2/4	; シリンダ数からバイト数/4に変換
		SUB		ECX,512/4		; IPLの分だけ差し引く
		CALL	memcpy

; asmheadでしなければいけないことは全部し終わったので、
;	あとはbootpackに任せる

; bootpackの起動

		MOV		EBX,BOTPAK
		MOV		ECX,[EBX+16]
		ADD		ECX,3			; ECX += 3;
		SHR		ECX,2			; ECX /= 4;
		JZ		skip			; 転送するべきものがない
		MOV		ESI,[EBX+20]	; 転送元
		ADD		ESI,EBX
		MOV		EDI,[EBX+12]	; 転送先
		CALL	memcpy
skip:
		MOV		ESP,[EBX+12]	; スタック初期値
		JMP		DWORD 2*8:0x0000001b

waitkbdout:
		IN		 AL,0x64
		AND		 AL,0x02
		JNZ		waitkbdout		; ANDの結果が0でなければwaitkbdoutへ
		RET

memcpy:
		MOV		EAX,[ESI]
		ADD		ESI,4
		MOV		[EDI],EAX
		ADD		EDI,4
		SUB		ECX,1
		JNZ		memcpy			; 引き算した結果が0でなければmemcpyへ
		RET
; memcpyはアドレスサイズプリフィクスを入れ忘れなければ、ストリング命令でも書ける

		ALIGNB	16
GDT0:
		RESB	8				; ヌルセレクタ
		DW		0xffff,0x0000,0x9200,0x00cf	; 読み書き可能セグメント32bit
		DW		0xffff,0x0000,0x9a28,0x0047	; 実行可能セグメント32bit（bootpack用）

		DW		0
GDTR0:
		DW		8*3-1
		DD		GDT0

		ALIGNB	16
bootpack:
